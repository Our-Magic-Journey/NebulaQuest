sourceCompatibility = JavaVersion.VERSION_16
sourceSets.main.java.srcDirs = [ "src/" ]
sourceSets.main.resources.srcDirs = ["../assets"]

mainClassName = "xyz.magicjourney.nebulaquest.desktop.DesktopLauncher"
def osName = System.getProperty('os.name').toLowerCase(Locale.ROOT)
project.ext.assetsDir = new File("../assets")

import org.gradle.internal.os.OperatingSystem

tasks.register('runDev', JavaExec) {
    dependsOn classes
    mainClass = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.assetsDir
    ignoreExitValue = true

    if (OperatingSystem.current() == OperatingSystem.MAC_OS) {
        // Required to run on macOS
        jvmArgs += "-XstartOnFirstThread"
    }
}

tasks.register('debug', JavaExec) {
    dependsOn classes
    mainClass = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.assetsDir
    ignoreExitValue = true
    debug = true
}

tasks.register('dist', Jar) {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    manifest {
        attributes 'Main-Class': project.mainClassName
    }
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
    destinationDirectory = file("$buildDir/lib")
}

tasks.register('copyAssets') {
    dependsOn jpackageImage

    if (osName == "linux") {
        project.mkdir "./build/jpackage/desktop/bin/assets"
        copy {
            from '../assets'
            into './build/jpackage/desktop/bin/assets'
        }
    }
    else {
        project.mkdir "./build/jpackage/desktop/assets"
        copy {
            from '../assets'
            into './build/jpackage/desktop/assets'
        }
    }

    project.mkdir "../release"
}

tasks.create('zipRelease', Zip) {
    dependsOn jpackageImage

    from './build/jpackage/desktop'

    if (osName == "linux") {
        archiveFileName.set("nebula-quest-linux.zip");
    }
    else {
        archiveFileName.set("nebula-quest-windows.zip");
    }

    destinationDirectory.set(file('../release'))
}

tasks.create('release') {
    dependsOn copyAssets
    dependsOn zipRelease
}

jpackageImage.dependsOn dist
dist.dependsOn classes

eclipse.project.name = appName + "-desktop"

runtime {
    options = ['--strip-debug',
               '--compress', '2',
               '--no-header-files',
               '--no-man-pages',
               '--strip-native-commands',
               '--vm', 'server']
    modules = ['java.base' ,
               'java.desktop',
               'jdk.unsupported']
    distDir = file(buildDir)

    jpackage {
        //jpackageHome = '/usr/lib/jvm/open-jdk'
        mainJar = dist.archiveFileName.get()
        if (osName.contains('windows')) {
            imageOptions = ["--icon", file("../icons/logo.ico")]
        } else if (osName.contains('linux')) {
            imageOptions = ["--icon", file("../icons/logo.png")]
        } else if (osName.contains('mac')) {
            imageOptions = ["--icon", file("../icons/logo.icns")]
        }
    }
}